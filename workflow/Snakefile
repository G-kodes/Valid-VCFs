from snakemake.utils import min_version, validate
import os

min_version("6.0")


# DEFINE CONFIG FILE FOR SNAKEMAKE:
configfile: os.path.join("config", "config.json")


validate(config, os.path.join("..", "config", "config.schema.json"))


include: "rules/init.smk"

ruleorder:  CP > TABIX_INDEXING


DataPrepList = DataPrepRequirements()


rule all:
    """
    Request validation for all .vcf files given in the `resources/data` folder
    """

    input:
        expand(
            "results/VALIDATED/{filename}.{ext}",
            filename=DataPrepList["ALL"],
            ext=["vcf.gz", "vcf.gz.tbi"],
        ),

rule CP:
    """
    Copy any VCF files that have already been BG-Zipped and have a Tabix Index accross
    """

    log:
        "results/PREPARED/CP_{name}.log",
    input:
        "resources/data/{name}",
    output:
        "results/PREPARED/{name}",
    conda:
        "envs/GATK4.yaml",
    shell:
        """
        cp {input} {output}
        """


rule BGZIP:
    """
    BGZIP a given un-zipped VCF file.
    """

    log:
        "results/PREPARED/BGZIP_{name}.log",
    input:
        "resources/data/{name}.vcf",
    output:
        "results/PREPARED/{name}.vcf.gz",
    conda:
        "envs/GATK4.yaml",
    resources:
        cpus=10,
        nodes=1,
        queue="long",
        walltime="900:00:00",
    shell:
        """
        bgzip < {input} > {output}
        """


rule TABIX_INDEXING:
    """
    Generate a Tabix index for the given VCF file
    """

    log:
        "results/PREPARED/TABIX_INDEXING_{name}.log",
    input:
        "results/PREPARED/{name}.vcf.gz",
    output:
        "results/PREPARED/{name}.vcf.gz.tbi",
    conda:
        "envs/GATK4.yaml",
    resources:
        cpus=10,
        nodes=1,
        queue="long",
        walltime="900:00:00",
    shell:
        """
        tabix -f -p vcf {input}
        """


rule FORMAT_VALIDATION:
    """
    Perform spec validation of VCF format.
    """

    log:
        "logs/VALIDATED/FORMAT_VALIDATION_{name}.log",
    input:
        vcf = "results/PREPARED/{name}.vcf.gz",
        tabix = "results/PREPARED/{name}.vcf.gz.tbi",
    output:
        "results/VALIDATED/{name}.vcf.gz",
        "results/VALIDATED/{name}.vcf.gz.tbi",
    params:
        refPath=config["Reference Genome"]["Folder Name"],
        refName=config["Reference Genome"]["Name"] + ".fa.gz",
        dbsnp="resources/reference_snps/" + config["Reference SNPs"]["Filename"],
        memory="128G",
        gatk=config['Software']['GATK']['Executable']
    resources:
        cpus=10,
        nodes=1,
        queue="long",
        walltime="900:00:00",
    conda:
        "envs/GATK4.yaml"
    shell:
        """
        {params.gatk} ValidateVariants -V {input.vcf} -R resources/{params.refPath}/{params.refName} -D {params.dbsnp}
        """